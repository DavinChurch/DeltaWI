 value←obj ∆WGet props;names;real
⍝ Get properties from a given object (each must potentially be an object property)
⍝ For use by common-area routines to access properties that may or may not exist on the particular object

 :If 1=≡⊃⊆props ⋄ props←⊂⊆props ⋄ :EndIf ⍝ This is a simple name or name-value pair so must nest
 value←names←⊃¨props←,props ⍝ Dyalog property names being referenced
 :If 0∊≢props ⋄ :Return ⋄ :EndIf ⍝ Nothing to do
 'Missing property name'⎕SIGNAL 11/⍨0∊≢¨props ⍝ Watch for internal coding error
 'Missing property name'⎕SIGNAL 11/⍨0∊≢¨names ⍝ Watch for internal coding error

 ⍝ For any real properties, just call ∆WG normally
 :If ∨/real←names∊⍥⎕C obj ∆WG'PropList' ⍝ (Hopefully this is fast enough to not need caching)
     (real/value)←obj ∆WG real/names ⍝ Obtain actual property values
 :EndIf

 ⍝ For any pseudo-properties, assume they're property-like variables and retrieve them (using any provided defaults)
 :If ∨/~real
     (value/⍨~real)←obj ⎕VGET props/⍨~real ⍝ Obtain from WS if present, otherwise use default value
 :EndIf
