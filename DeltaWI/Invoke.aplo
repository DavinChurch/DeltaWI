 result←obj(action Invoke)args;handler;fn;nothere;op
 ⍝ Invoke one of the class-based operational functions within the Classes namespaces.
 ⍝ Search the class-specific namespace first, then the class-generic namespace.
 ⍝ Execute the found function with the given arguments and return the result back to ∆WI.
 ⍝ If not found, signal an error.

 fn←'⍙'@('⎕'=⊢)action                           ⍝ If we allow synthetic ⎕-names, use ⍙ in the fn name instead
 nothere←'∆WI ACTION ERROR: Action "',action,'" not found'
 nothere Error(~IsValidName fn)∨∨/'∆⍙_'∊action  ⍝ Action names must be valid, but cannot contain special "letters"

 ⍝ Decide upon the needed routine for action execution
 :If 4=obj._Class.⎕NC op←'Invoke'               ⍝ If there's an Invoke operater here, we use it in preference to fixed-name definitions
     ⍝ This operator is used by ActiveX & Dyalog class-types, as well as Grid and APL special case rewrites
     nothere Error 1 2 1≢⊃obj._Class.⎕AT op     ⍝ Report error when finding a variable definition with improper arguments
     handler←action obj._Class.Invoke           ⍝ Use the pre-bound invocation operator for external classes
 :ElseIf 3=obj._Class.⎕NC fn                    ⍝ Is there a class-specific function?
     nothere Error 1 2 0≢⊃obj._Class.⎕AT fn     ⍝ Report error when finding a blocked general definition
     handler←obj._Class.⍎fn                     ⍝ Obtain reference to the handler function (I wish there was a non-⍎ way to do this)
 :ElseIf 3=Classes.⎕NC fn                       ⍝ Is there a class-generic function?
     nothere Error 1 2 0≢⊃Classes.⎕AT fn        ⍝ Report error when finding a general definition with improper arguments
     handler←Classes.⍎fn                        ⍝ Obtain reference to the handler function (I wish there was a non-⍎ way to do this)
 :Else
     Error nothere
 :EndIf

 ⍝ Now, invoke the actual processing routine to do the job!
 result←obj handler args
