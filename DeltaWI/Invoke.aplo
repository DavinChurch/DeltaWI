 result←obj(action Invoke)args;handler;fn
 ⍝ Invoke one of the class-based operational functions within the Classes namespaces.
 ⍝ Search the class-specific namespace first, then the class-generic namespace.
 ⍝ Execute the found function with the given arguments and return the result back to ∆WI.
 ⍝ If not found, signal an error.

 fn←'⍙'@('⎕'=⊢)action               ⍝ If we allow synthetic ⎕-names, use ⍙ in the fn name instead
 :If 3=obj.Class.⎕NC fn             ⍝ Is there a class-specific fn?
     ('∆WI ACTION ERROR: Action "',action,'" not found')Error 1≥≢obj.Class.⎕CR fn ⍝ Report overridden definition
     handler←obj.Class.⍎fn          ⍝ Obtain reference to fn
 :ElseIf 3=Classes.⎕NC fn           ⍝ Is there a class-generic fn?
     handler←Classes.⍎fn            ⍝ Obtain reference to fn
 :Else
     Error'∆WI ACTION ERROR: Action "',action,'" not found'
 :EndIf

 ⍝ Now, invoke the actual processing routine!
 result←obj handler args
