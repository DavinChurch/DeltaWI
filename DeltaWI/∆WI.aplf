 result←{object}∆WI action;Application;objname;objref;args;name;⎕TRAP;GUIroot;GUIref
⍝ Interface point of DeltaWI package -- emulates APL+Plus ⎕WI system function

 ⎕TRAP←⊂870 'C' '⎕EN ⎕SIGNAL⍨⊃⎕DM'
 Application←⊃⎕THIS,⍨⎕RSI~⎕THIS                                     ⍝ Global: Where are we being called from?
 GUIroot←'#.⍙WI'                                                    ⍝ Global: Where we build our GUI structure (for later text reference)
 :If 0=⎕NC GUIroot ⋄ CreateRoot GUIroot ⋄ :EndIf                    ⍝ Global: On our first call, create a root from which everything GUI grows
 GUIref←#.⍙WI                                                       ⍝ Global: A reference to our GUI structure root (for later use)

⍝ Pre-process the ∆WI call
 :If 0=⎕NC'object' ⋄ object←⊢ ⋄ :EndIf                              ⍝ Allow monadic use
 (objname objref action)←object ObjectName action                   ⍝ Parse object name to which this call applies
 :If action≡,'*' ⋄ ∘∘∘ ⋄ :EndIf                                     ⍝? Resume from within onAction handler? [Not yet designed]
 (name args)←Normalize action                                       ⍝ Separate action name from its arguments and standardize structure
 name←objref Canonize name                                          ⍝ In cases where capitalization is optional, adjust it to standard
 :If ⎕NULL≢result←name(objref Immediate)args ⋄ :Return ⋄ :EndIf     ⍝ Perform any out-of-band "instant" processing
 :If ⎕NULL≢result←action(objref OnAction)args ⋄ :Return ⋄ :EndIf    ⍝ Perform any application-specified alternative onAction processing

⍝ Time to execute the requested operation
⍝ Handle object creation cases separately; otherwise start up the normal action processing by class
 :Select name
 :Case 'Create' ⋄ result←objref(objname New.Create)args             ⍝ Delete any existing GUI object and then recreate it
 :Case 'New' ⋄ result←objref(objname New.New)args                   ⍝ Create a new GUI object as a child of its parent objref
 :Else ⋄ result←objref(name Invoke)args                             ⍝ Call the standard processing routine (by proxy), passing standard arguments
 :EndSelect
