 av←font QuadAV unicode;t;reverse;letter;fnt
⍝ If the font is an APL+Win font (based on ⎕AV), then convert the Unicode right argument to ⎕AV-based text
⍝ Only ⎕AV characters can be converted in this way. Non-⎕AV characters produce "default" results.
⍝ Unicode characters that are not in ⎕AV produce ¯1↑⎕AV as a result.
⍝ These defaulted characters return ⎕UCS 160 when converted in the reverse direction.
⍝ Therefore there is a loss of content when converting non-⎕AV characters (in both directions).

⍝ Provide the font name with a '←' prefix in order to signal that ⎕AV characters are to be convert back to Unicode.
⍝ (An optional '→' prefix is allowed for the "forward" conversion, but it is ignored.)
⍝ In the usual case of supplying a font name, an object reference may be supplied instead for the _font to be extracted from it.

 av←unicode ⍝ Normally just return whatever they gave us, since most applications won't be using APL+Win fonts
 :If 9=⎕NC'font'
     fnt←∆dmu⊃font ⎕VGET⊂'_font' '' ⍝ Get it directly from the object (but can't reverse it this way)
 :Else
     fnt←∆dmu font
 :EndIf

 :If fnt⊂⍛∊⍥⎕C'APL+Win' 'APL2000' 'APLHELP' 'APLPLUS' 'APLPLUSI' ⍝ List of known APL+Win font names
     reverse←'←'∊fnt ⋄ fnt~←'←→' ⍝ Watch out for switching it back to Unicode
     letter←0≢¨⊃0⍴⊂unicode
     :If reverse
         av←letter\TranslateAV[⎕IO+255⌊⎕UCS letter/unicode] ⍝ Return the ⎕UCS character that matches the selected ⎕AV glyph
     :Else
         av←⎕UCS ⎕IO-⍨(≢TranslateAV)⌊TranslateAV⍳unicode ⍝ Return the ⎕UCS code where the appropriate glyph is found in ⎕AV
     :EndIf
     :If ∨/t←~letter ⋄ (t/av)←t/unicode ⋄ :EndIf ⍝ Check to see if any of their characters were explicit Unicode (numeric) and preserve them exactly
     ⍝ This should support data that should be valid for ⎕WI, but APL+Win has another form of Unicode that can be generated by
     ⍝ their ⎕UCS function. I don't think that their Unicode datatype is supported by ⎕WI at all, but if it turns out that it is needed
     ⍝ then something will need to be added here.
 :EndIf
 ⍝? Esoteric question: Is a constant string already in native-Unicode characters converted when it's ported?? Does that matter to us anyway?
