 av←font QuadAV unicode;t;reverse;letter;fnt
⍝ If the font is an APL+Win font (based on ⎕AV), then convert the Unicode right argument to ⎕AV-based text codes
⍝ Only ⎕AV characters can be converted in this way. Non-⎕AV characters produce "default" results.
⍝ Unicode characters that are not in ⎕AV produce ¯1↑⎕AV (non-breaking space) as a result.
⍝ A non-breaking space converts back to the equivalent in Unicode (⎕UCS 160) when converted in the reverse direction.
⍝ Any provided characters that are not valid APL+Win characters (out of their scope) are reverse-converted to ⎕UCS 160 as well.
⍝ Therefore there is a loss of content when converting non-⎕AV characters (in either direction).

⍝ Provide the font name with a '←' prefix in order to signal that ⎕AV characters are to be convert back to Unicode.
⍝ (An optional '→' prefix is allowed for the "forward" conversion, but it is ignored.)
⍝ In the most common case of converting to a font name, a GUI object reference may be supplied instead of the name itself,
⍝ and the _font variable will be extracted from it automatically.

 av←unicode ⍝ Normally just return whatever they gave us, since most applications won't be using APL+Win fonts
 :If 9=⎕NC'font'
     fnt←∆dmu⊃font ⎕VGET⊂'_font' '' ⍝ Get it directly from the object (but can't reverse it this way)
 :Else
     fnt←∆dmu font
 :EndIf

 reverse←'←'∊fnt ⋄ fnt~←'←→' ⍝ Watch out for switching it back to Unicode
 :If fnt⊂⍛∊⍥⎕C'APL+Win' 'APL2000' 'APLHELP' 'APLPLUS' 'APLPLUSI' ⍝ List of known APL+Win font names
     letter←0≢¨⊃0⍴⊂unicode
     :If reverse ⍝ Return the ⎕UCS character that matches the selected ⎕AV glyph
         av←letter⍴⍛⍴letter\⍥,⎕UCS UCStoAV[⎕IO+160@{~⍵∊0,⍳255}61440-⍨⎕UCS letter/⍥,unicode] ⍝ Fill unknowns with non-breaking space
     :Else ⍝ Return the ⎕UCS code where the appropriate glyph is found in ⎕AV
         av←⎕UCS 61440+160@{⍵=≢UCStoAV}⎕IO-⍨UCStoAV ⎕UCS⍛⍳unicode ⍝ Fill unknowns with non-breaking space
     :EndIf
     :If ∨/t←~,letter ⋄ (t/,av)←t/,unicode ⋄ :EndIf ⍝ Check to see if any of their characters were explicit Unicode (numeric) and preserve them exactly
     ⍝ This should support data that should be valid for ⎕WI, but APL+Win has another form of Unicode that can be generated by
     ⍝ their ⎕UCS function. I don't think that their Unicode datatype is supported by ⎕WI at all, but if it turns out that it is needed
     ⍝ then something will need to be added here.
 :EndIf
