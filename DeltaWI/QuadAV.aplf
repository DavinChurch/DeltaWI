 av←font QuadAV unicode;t
⍝ If the font is an APL+Win font (based on ⎕AV), then convert the Unicode right argument to ⎕AV-based text

 av←unicode ⍝ Normally just return whatever they gave us, since most applications won't be using APL+Win fonts

 :If 'APL+Win' 'APL2000' 'APLHELP' 'APLPLUS' 'APLPLUSI'∊⍨⊂font ⍝ List of known APL+Win font names
     av←⎕UCS ⎕IO-⍨(≢∆AV)⌊∆AV⍳unicode ⍝ Return the ⎕UCS code where the appropriate glyph is found in ⎕AV
     :If ∨/t←0≡¨⊃0⍴⊂unicode ⋄ (t/av)←t/unicode ⋄ :EndIf ⍝ Check to see if any of their characters were explicit Unicode (numeric) and preserve them exactly
     ⍝ This should support data that should be valid for ⎕WI, but APL+Win has another form of Unicode that can be generated by
     ⍝ their ⎕UCS function. I don't think that their Unicode datatype is supported by ⎕WI at all, but if it turns out that it is needed
     ⍝ then something will need to be added here. ⍝? Is a constant string already in native-Unicode characters converted when it's ported??
 :EndIf
