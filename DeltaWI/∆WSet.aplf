 testing←obj ∆WSet props;names;real
⍝ Set properties on a given object (each must potentially be an object property)
⍝ For use by common-area routines to access properties that may or may not actually exist on the particular object

⍝ Begin by ensuring correct nesting for each property
 :If 0=≡props ⋄ props←,¨props ⋄ :EndIf
 :If 1=|≡props ⋄ props←⊂,props ⋄ :EndIf
 props←,¨⊆¨,props
 :If obj≡'⎕' ⋄ testing←props ⋄ :Return ⋄ :EndIf ⍝ Only for testing argument parsing
 names←,¨⊃¨props ⍝ Dyalog property names being referenced
 :If 0∊≢props ⋄ :Return ⋄ :EndIf ⍝ Nothing to do
 'Missing property name'⎕SIGNAL 11/⍨0∊≢¨props ⍝ Watch for internal coding error
 'Missing property name'⎕SIGNAL 11/⍨0∊≢¨names ⍝ Watch for internal coding error
 'Missing property value'⎕SIGNAL 11/⍨∨/1≥≢¨props ⍝ Watch for internal coding error

 ⍝ For any real properties, just call ∆WS normally
 :If ∨/real←names∊⍥⎕C obj ∆WG'PropList' ⍝ (Hopefully this is fast enough to not need caching)
     obj ∆WS real/props ⍝ Set actual property values
 :EndIf

 ⍝ For any pseudo-properties, create property-like variables instead in case of later need
 :If ∨/~real
     obj ⎕VSET real~⍛/props ⍝ Set WS value instead
 :EndIf
