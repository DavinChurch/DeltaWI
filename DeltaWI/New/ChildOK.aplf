 ok←parent ChildOK child;children;parents;in;class;subclass
⍝ Determine if the given parentage relationship is permitted by the defined rules in _supp.∆Children & _supp.∆Parents.
⍝   parent←Reference to parent GUI object
⍝   in←child[1]←Reference to new child class definition
⍝   class←child[2]←Class name of new child
⍝   subclass←child[3]←Optional name of ActiveX or Dyalog subclass
⍝   ok←Parentage permitted

 (in class subclass)←3↑child,⊂''
⍝? What can we do to implement subclass permissions in ActiveX/Dyalog?
⍝? Search for subclasses in a different structure of global variable in those NSs (if subclass≢'')?
 ('∆WI CLASS ERROR: Class "',class,'" not found')##.Error 3≠in.⎕NC'New'
 children←⍬ ⋄ :If 2 3∊⍨parent.Class.⎕NC'_supp.∆Children' ⋄ children←,,¨⊆parent.Class._supp.∆Children ⋄ :EndIf
 parents←⍬ ⋄ :If 2 3∊⍨in.⎕NC'_supp.∆Parents' ⋄ parents←,,¨⊆in._supp.∆Parents ⋄ :EndIf
 children,⍨←((×≢children)∧∧/'~'∊¨children)/⊂'*' ⍝ If they're all exclusions, assume they're the only exclusions from entire list
 parents,⍨←((×≢parents)∧∧/'~'∊¨parents)/⊂'*' ⍝ If they're all exclusions, assume they're the only exclusions from entire list
 :If ~ok←>/(2↑children∊⍨⊂,'*')∨('' '~',¨⊂class)∊children ⍝ Is this new class permitted by the parent?
     ok←>/(2↑parents∊⍨⊂,'*')∨('' '~',¨⊂parent._class)∊parents ⍝ Is the parent class permitted by the new child?
 :EndIf
