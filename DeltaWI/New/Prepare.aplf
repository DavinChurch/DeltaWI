 result←object Prepare arg;sub;item;illegal;final;v0;vars
 ⍝ Finish initializing a just-created new GUI object
 ⍝ The action and class have already been removed from the argument list.

 ⍝ Watch out for things that can't be handled on a New-command
 illegal←'Create' 'New' ⍝? What else might be illegal here (by name)?
 '∆WI OBJECT ERROR: Object already exists'##.Error∨/illegal∊⊃¨⊆¨arg

 object._self↓⍨←2×'#.'≡2↑object._self ⍝ Make sure that _self doesn't have a leading '#.' on it (may be created by top-level New functions)

 object ##.∆WSet('Coord' 'RealPixel')('AutoConf' 0)('Visible' 0) ⍝ Everybody needs these (that have them)
 :Select object._class
 :Case 'Menu'
     ⍝ An APL+Win Menu object may be any of four Dyalog object-types depending on context.
     ⍝ That much should have been handled by Menu.New by now.
     ⍝? Hmmm... will Menu.New create the MenuBar part of a top-level menu (probably not) or the named Menu item upon it? Start hidden and relocate??
     ⍝? Can we look backwards (up the stack) to handle this properly?
     ⍝? Is there anything else we need to do specially here?
 :Case 'MDIForm'
     sub←⎕VGET((⍕object),'._mdi')##.∆WC'MDIClient'('Coord' 'RealPixel')('AutoConf' 0)
     sub._Owner←object
 :Case 'Picture'
     object ##.∆WS'Coord' 'User' ⍝ A picture object's parent needs an exception to Coord
     sub←⎕VGET((⍕object),'._pic')##.∆WC'SubForm'('Coord' 'RealPixel')('AutoConf' 0)
     sub._Owner←object
     ⍝? Now we're going to need to attach a file-less Bitmap object so this can be drawn on.
 :EndSelect

 ⍝ Assign class default pseudo-properties and variables
 object.(_scale _opened _visible)←1 0 0 ⍝ All new objects default to scale=1, not-yet-logically-open, and invisible
 vars←object._Class.⎕NL ¯2.1 ⍝ What variables are in this namespace (including currently-executing locals)
 vars←(~1∊¨⊃¨object._Class.⎕STATE¨vars)/vars ⍝ Remove currently-executing locals to leave only globals
 object ⎕VSET(↑vars)(object._Class ⎕VGET vars) ⍝ Replicate them in the new object

 ⍝ Apply list of application-specific properties and methods to this new object
 final←'Close' 'Hide' 'Show' 'Open' ⍝ Methods that are not done until the very end (the last one being the default)
 :For item :In arg~final
    ⍝ We'll handle these by calling ∆WI recursively and hope we won't get interrupted or slow down
     {}object._self ##.∆WI item ⍝ Call ∆WI to do the work, but we can never use a returned result from these operations
 :EndFor

 ⍝ Activate/show the new object as requested
 :For item :In final∩⍨arg,(~∨/final∊arg)/¯1↑final ⍝ There should only be one of these at most, usually; default=Open
⍝? Don't forget to fire automatic events for these methods (respect suppress??)
⍝? Watch out for possible inheriting from parentage?
     v0←-object._class⊂⍛∊'Form' 'MDIForm' 'Menu' ⍝ "Invisible" setting for this class
     :Select item
     :Case 'Close'
         object ##.∆WSet('Active' 0)('Visible' 0)
         object ⎕VSET('_opened' 0)('_visible'v0)('_enabled' 0)
     :Case 'Hide'
         object ##.∆WSet('Active' 1)('Visible' 0)
         object ⎕VSET('_opened' 1)('_visible'v0)('_enabled' 1)
     :Case 'Show'
         object ##.∆WSet('Active' 0)('Visible' 1)
         object ⎕VSET('_opened' 1)('_visible' 1)('_enabled' 1)
     :Case 'Open' ⍝ Or none of the above
         object ##.∆WSet('Active' 1)('Visible' 1)
         object ⎕VSET('_opened' 1)('_visible' 1)('_enabled' 1)
     :EndSelect
 :EndFor

 result←object._self ⍝ Finally, give them back the GUI name of the object that they created
