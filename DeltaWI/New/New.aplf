 result←object New arg;name;parent;class;meta;ismeta;isreal;isnew;isnative;in;subclass;hidden
⍝ Create a brand-new GUI object, as specified

⍝? Future support: We should be able to pass a GUI 'def' variable as the argument. (This will probably be a ⎕OR under Dyalog)

 :Section ⍝ Analyze arguments to see what kind of processing decisions we need to make below
     (parent name)←object ⍝ Separate non-standard two-part argument
     '∆WI OBJECT ERROR: Object already exists'##.Error(⊂name)∊parent ##.Classes.children ⍬ ⍝ Make sure the name does not yet exist
     'LENGTH ERROR'##.Error 0=≢arg
     '∆WI CLASS NAME ERROR: Improperly formed class name'##.Error 0=≢class←##.∆dmu⊃arg
     class↓⍨←isnative←'*'=⊃class ⍝ Are we signalling a bypass of newclasses?
     ismeta←×≢meta←(' '∊class)/⊃subclass←' '##.∆cut class ⍝ Look for the first word in a multi-word class name to see if it needs special group (meta-class) handling
     subclass←1↓subclass ⍝ Hang onto the second word (if present) so we can try to use it for additional authorization checking
     isreal←(⊂class)∊##.GUIref ##.Classes.System.classes ⍬ ⍝ Standard system class names
     isnew←(⊂class)∊##.GUIref ##.Classes.System.newclasses ⍬ ⍝ Application-named UDC classes
     arg←,¨⊆¨arg ⍝ Pre-nest & ravel entire argument list for passing to .New routines
 :EndSection

 :Section ⍝ See if we've got a hidden Dyalog-required GUI child under this one, which should become our future Dyalog GUI parent
     hidden←'_mdi' '_pic' ⍝ Possible hidden object names that need to remain as part of the GUI structure
     ⍝ Note that an _mb cannot yet exist on a new object, so we won't worry about them here
     :If ×≢hidden←(9⍳⍨parent.⎕NC↑hidden)⊃hidden,⊂'' ⍝ Do one of these exist here?
         object[1]←parent ⎕VGET hidden ⍝ If so, change the future actual parent reference to that object instead
     :EndIf
 :EndSection

 ⍝ Check all the different combination possibilities to see how we should analyze this
 :If ismeta ⍝ We have a special *type* of class
     :Section ⍝ This needs to be processed as a meta-class
         :Select meta ⍝ Must be a case-sensitive type-name that we support
         :CaseList 'ActiveControl' 'ActiveObject' ⍝? These spellings aren't case-insensitive, are they?
             in←##.Classes.ActiveX ⍝ Just for easier reference
             :If parent ChildOK in class subclass ⍝ Determine if we can place this child here
                 result←object in.New arg ⍝ Call the ActiveX-specific New function
             :Else
                 ##.Error'∆WI PARENTAGE ERROR: Invalid parentage for class "',class,'"'
             :EndIf
         :Case 'Dyalog'
             in←##.Classes.Dyalog ⍝ Just for easier reference
             :If parent ChildOK in class subclass ⍝ Determine if we can place this child here
                 result←object in.New arg ⍝ Call the ActiveX-specific New function
             :Else
                 ##.Error'∆WI PARENTAGE ERROR: Invalid parentage for class "',class,'"'
             :EndIf
         :Else
             ##.Error'∆WI CLASS ERROR: Class "',class,'" not found'
         :EndSelect
     :EndSection

 :ElseIf (~isnative)∧(~'.'∊class)∧isnew∨~isreal ⍝ Or they've told us or implied to process it as new
     :Section ⍝ Application-controlled UDC creation
         in←##.UDC ⍝ Just for easier reference
         result←object in.New arg ⍝ Call the UDC-specific New function
     :EndSection

 :ElseIf isreal ⍝ Then they haven't replaced a standard class name
     :Section ⍝ Process this as a normal class
         in←##.Classes ⎕VGET⊂class ⎕NULL ⍝ Grab a reference to the proper standard class namespace (it can't accidentally grab a fn)
         ('∆WI CLASS ERROR: Class "',class,'" not found')##.Error in≡⎕NULL ⍝ Make sure it exists
         :If parent ChildOK in class ⍝ Determine if we can place this child here
             result←object in.New arg ⍝ Call the class-specific New handler
         :Else
             ##.Error'∆WI PARENTAGE ERROR: Invalid parentage for class "',class,'"'
         :EndIf
     :EndSection

 :ElseIf '.'∊class ⍝ Check for an implied ActiveX control here ⍝? (is this the best way to check?)
     :Section ⍝ This must be an implied meta-class
         in←##.Classes.ActiveX ⍝ Just for easier reference
         :If parent ChildOK in class subclass ⍝ Determine if we can place this child here
             result←object in.New arg ⍝ Call the ActiveX-specific New function
         :Else
             ##.Error'∆WI PARENTAGE ERROR: Invalid parentage for class "',class,'"'
         :EndIf
     :EndSection

 :ElseIf ×≢##.GUIref ⎕VGET⊂'_onNew' ''
     ⍝ Finally, before giving up, try running onNew anyway to see if an unknown class can be created
     :Section ⍝ Application-controlled UDC creation
         in←##.UDC ⍝ Just for easier reference
         result←object in.New arg ⍝ Call the UDC-specific New function
     :EndSection

 :Else
     ##.Error'∆WI CLASS ERROR: Class "',class,'" not found'
 :EndIf
