 result←object(realevent SetEvent myevent)arg;after;before;event;pseudo;t;bat;aat
⍝ Set up event-handling in a standard way

⍝ Left and right arguments are provided directly from the ∇onXxxx setting function
⍝ All event names must be provided with case-sensitive canonical names (without "on" prefix)
⍝ Left operand is a single Dyalog event name to be intercepted
⍝ Right operand is an APL+Win event name, optionally nested and followed by ordering criteria
⍝   Ordering criteria should be one or more other APL+Win event names prefixed by either "<" or ">".
⍝   The new event should be processed *after* any events named with a ">" prefix and *before* any events named with a "<" prefix.
⍝ Example: result←object ('Select' ##.##.SetEvent 'Click' '>MouseDown' '>MouseUp') arg

 :Section ⍝ Validate arguments (use ⎕SIGNAL for SetEvent usage errors)
     myevent←,⊆myevent ⋄ before←1↓¨myevent/⍨'<'=⊃¨myevent ⋄ after←1↓¨myevent/⍨'>'=⊃¨myevent ⋄ myevent/⍨←~'<>'∊⍨⊃¨myevent ⍝ Decompose event list
     'No emulated event name provided'⎕SIGNAL 5/⍨0=≢myevent
     'Too many emulated event names provided'⎕SIGNAL 5/⍨1<≢myevent
     myevent←⊃myevent ⍝ We only set up one emulated event with a call here
     'LENGTH ERROR: Wrong number of arguments'Error 1<≢arg
     'Improper specification of event names'⎕SIGNAL 11/⍨0∊IsChar¨realevent myevent,before,after
     'Do not use "on" prefix on event names'⎕SIGNAL 11/⍨(⊂'on')∊2↑¨realevent myevent,before,after
     'Not an available native event name'⎕SIGNAL 11/⍨~realevent∊⍥⊆object ∆WG'EventList'
     result←Classes.NoRes ⍝ We'll pre-set ahead of time in case of an early exit
 :EndSection

 :Section ⍝ Return existing handler if requested
     :If 0=≢arg
         result←object ⎕VGET⊂('_on',myevent)'' ⍝ Retrieve existing APL+Win event handler text
         :Return
     :EndIf
 :EndSection

 :Section ⍝ Record APL+Win event handler text (exactly as specified)
     ('∆WI DOMAIN ERROR: on',myevent)Error~IsChar arg←⊃arg
     ('∆WI RANK ERROR: on',myevent)Error 1≠≢⍴arg←1/arg
     object ⎕VSET⊂('_on',myevent)arg ⍝ Set new APL+Win event handler text
 :EndSection

 :Section ⍝ Obtain or initialize Dyalog-native event definition
     event←0~⍨object ∆WG'Event'
     :If (≢event)≥t←realevent⍳⍨⍥⊆{⍵↓⍨2×'on'≡2↑⍵}¨⊃¨event
         event←t⊃event ⍝ Obtain existing event definition
     :Else
         event←('on',realevent)((⍕⎕THIS),'.Event')⍬ ⍝ Create a new event definition
     :EndIf
     pseudo←0~⍨3⊃event ⍝ Extract out just the event-processor argument of APL+Win event names to be modified
 :EndSection

 :Section ⍝ Update list of events to emulate here
     :If 0=≢arg ⍝ Are they disabling an event?
         pseudo~←⊂myevent ⍝ Remove the name from the list to process
         ⍝? What happens if we're removing the last APL+Win event from this Dyalog event?
     :ElseIf myevent∊⍥⊆pseudo
         ⍝ It's already here - leave it alone
         :Return ⍝ We don't need to update anything in the GUI
     :Else
         bat←⌊/(1+≢pseudo),pseudo⍳before ⍝ Where are any before parameters?
         aat←⌈/0,(1+≢pseudo)|pseudo⍳after ⍝ Where are any after parameters?
         'Cannot satisfy positioning requirements'⎕SIGNAL 11/⍨bat≤aat
         :If bat>≢pseudo ⍝ There were no "before" parameters
             :If aat=0 ⍝ There were no "after" parameters
                 pseudo,←⊂myevent ⍝ Just append it to the end
             :Else ⍝ We have only "after" parameters
                 pseudo←(aat↑pseudo),(⊂myevent),aat↓pseudo ⍝ Put it right after the required parameter (since they're likely to be related)
             :EndIf
         :Else ⍝ We have "before" parameters
             :If aat=0 ⍝ But no "after" parameters
                 pseudo←(pseudo↑⍨bat-1),(⊂myevent),pseudo↓⍨bat-1 ⍝ Insert new event at (the last) acceptable position
             :Else ⍝ We also have "after" parameters
                 pseudo←(aat↑pseudo),(⊂myevent),aat↓pseudo ⍝ Put it right after the required parameter (but could just as easily go at the other end)
             :EndIf
         :EndIf
     :EndIf
 :EndSection

 :Section ⍝ Replace Dyalog-native event definition
     (3⊃event)←pseudo ⍝ Replace list of APL+Win event names to process
     object ∆WS'Event'event ⍝ Set new or updated native-event specification (merges with existing list)
 :EndSection
