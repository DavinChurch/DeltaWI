 CreateRoot root;inches;this;tpp;devcaps
⍝ The first time that ∆WI is used, we need to create and populate the root of the GUI structure

 this←⎕VGET root ⎕NS'' ⍝ Create the root GUI namespace that we can build from

 this.(_name _self _class)←,¨'#' '#' 'System' ⍝ Constant properties
 this._Ancestry←0⍴this._Class←Classes.System ⍝ Base references

 ⍝ Define system settings
 devcaps←'.'⎕WG'DevCaps'
 this.ScreenSize←1⊃devcaps ⍝ The pixel-size of the entire screen
 this.AspectRatio←÷/⌽2⊃devcaps ⍝ The ratio (W÷H) of inch-size of the screen; estimated; used for visual concerns like square- and circle-drawing
 ⍝inches←(2⊃devcaps)÷25.4 ⍝ How big is the screen in inches (h,w); estimated
 tpp←15 ⍝?1440÷25.4×⊃÷/2↑devcaps ⍝ Twips/pixel ⍝ Override calculated tpp with an assumed value (should we be doing this?)
 ⍝ _units definition (including any post-computed scale=0 factors), and variables derived from them, are always in real-pixels-per-unit
 this._units←1 2⍴16 8 ⍝ Pixels per system character unit (constant; assumed)
 this._units⍪←this._units[1;]÷8 4 ⍝ Dialog units
 this._units⍪←2⍴20÷tpp ⍝ screensize÷inches÷72 ⍝ Points (72/in)
 this._units⍪←2⍴÷tpp ⍝ Twips (1440/in)
 this._units⍪←devcaps[4]÷100 ⍝ Virtual pixels (scaled, by default)
 this._units⍪←1 1 ⍝ True pixels
 this._size←this.ScreenSize÷this._units[1;] ⍝ Represents the physical screen size, in character units
 this._where←0 0,this._size ⍝ Pretend the APL root has a location (full-screen)
 this._extent←this._size ⍝ Pretend the APL root has an extent (full-screen)
 this._scale←1 ⍝ Pretend the APL root is measured in characters
 ⍝ Dyalog GUI objects need a parent's Coord in order to measure Posn (at least)
 ⍝ The usual default for a Dyalog root is 'Prop', so convert it to/from pixels to match up (unless it has been changed)

 ⍝? We may eventually set up something here to make sure that onTooltip and onPrompt can be supported?

 ⍝ Assign class default properties
 this ⎕VSET Classes.System ⎕VGET ¯2
