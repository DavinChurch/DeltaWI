 result←object children arg;hidden;at;here
⍝ Produce a list of available children belonging to this object

 '∆WI ACTION ERROR: Property "children" is read-only'##.Error×≢arg
 :If 0=≢result←1↓(⊂''),⍕¨##.∆WN object ⋄ :Return ⋄ :EndIf
 :For hidden :In result∩'_mb' '_mdi' '_pic' ⍝ Search under known-invisible children
     at←result⍳⊂hidden
     here←1↓(⊂''),⍕¨##.∆WN object ⎕VGET hidden
     result←((at-1)↑result),here,at↓result
 :EndFor
 result/⍨←~('_'=⊃¨result)∧∧/¨result∊¨⊂⎕D ⍝ Eliminate numbered invisible children
 result↓¨⍨←1+≢⍕object ⍝ Don't include Dyalog's prefixes of this object
 result,⍨¨←⊂2↓object._self,'.' ⍝ Result must return full APL+Win GUI names (including APL+Win parentage), except '#.'
