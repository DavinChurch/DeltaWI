 {result}←object(force close)arg;hidden;kids
⍝ Close a GUI object and its children, if not denied, but do not delete it completely
⍝ Return a 1 for forced or successfully closed, or 0 if closing was application-denied.
⍝ The "force" operand specifies whether (0) or not (1) to run the onClose event and allow it to deny closing.

 :If result←0=object._opened ⋄ :Return ⋄ :EndIf ⍝ If it's already closed, then we just tell them so
 :If ~result←force ⍝ Do not run the onClose handler if closing is forced
     result←'Close'##.##.Event object ##.##.Application,arg ⍝ Run the onClose event handler (always return 0 or 1)
 :EndIf
 :If 0≢result←##.##.Single result ⋄ result←1 ⋄ :EndIf ⍝ Assume it was 1 if there's no handler or there's an invalid result
 :If 0=result ⋄ :Return ⋄ :EndIf ⍝ Closing was explicitly denied

 ⍝ Proceed to close the object, starting by force-closing all its children
 :If ×≢kids←object ##.children ⍬
 :AndIf ×≢kids←object ⎕VGET ##.##.LeafName¨kids ⍝ Except our own tree
     kids(1 close)¨⊂⍬ ⍝ Force-close all my children recursively
 :EndIf

 ⍝ Now, let's "destroy" myself
 'Destroy'##.##.Event object ##.##.Application ⍝ Run the onDestroy event handler

 ⍝ And now we can mark myself as gone, but not forgotten
 object._opened←0
 object ##.##.∆WSet('Active' 0)('Visible' 0) ⍝ Update Dyalog control properties
