 result←object Open arg;hidden;kids;waiting
⍝ Open a closed GUI object

 :If 0≠object._opened ⍝ If it's already opened/opening, then we have nothing to do here
     result←NoRes
     :Return
 :EndIf

 object._opened←¯1 ⍝ Mark it as "opening", briefly
 result←'Open'##.Event object ##.Application,arg ⍝ Run the onOpen event handler
 'Resize'##.Event object ##.Application,object._size,0 ⍝ Run the onResize event handler (always)
 'Paint'##.Event object ##.Application ⍝ Run the onPaint event handler (always)
⍝? What do we need to do about hidden internal objects while opening?
 object._opened←1 ⍝ Now it's fully open

 :If ×≢kids←object children ⍬
     result←(object ⎕VGET ##.LeafName¨kids)('Open'##.Invoke)¨⊂⍬ ⍝ Recursively Open all my children
 :EndIf

 object ##.∆WSet⊂'Active' 1 ⍝ Update Dyalog control properties
 :If object._enabled∧object._visible>0
     object ##.∆WSet⊂'Visible' 1 ⍝ Make sure it's shown
 :EndIf

 ⍝ If we're Opening a new top-level Form/MDIForm while a Wait is ongoing, then we need to add ourselves in as part of the Wait
 ⍝ (Hopefully this shouldn't be possible if it's already a member of another Wait.)
 :If object._class⊂⍛∊'Form' 'MDIForm' ⍝ I think only these can join a Wait
 :AndIf ×≢waiting←##.GUIroot ⎕VGET⊂'_Waiting'⍬ ⍝ If there's something currently waiting
 :AndIf ⎕NULL≡object ⎕VGET⊂'_WaitOn'⎕NULL ⍝ (Just in case; we'd better not be able to be Waiting with another Form)
     0 ##.∆NQ(⊃waiting)9258 object ⍬ ⍝ Add me to their active waiting list using the special on9258 event.
 :EndIf






⍝? Old coding I want to remember for a while...
⍝ :Section ⍝ Get rid of hidden objects
⍝     hidden←'_mb' '_mdi' '_pic'
⍝     :For hidden :In (9=object.⎕NC↑hidden)/hidden ⍝ See about getting rid of any hidden Dyalog immediate children
⍝         :Select hidden
⍝         :Case '_mb'
⍝⍝? Add any special handling for this
⍝         :Case '_mdi'
⍝⍝? Add any special handling for this
⍝         :Case '_pic'
⍝⍝? Add any special handling for this
⍝         :EndSelect
⍝         :If ×≢hidden←'_'(object ⎕VGET hidden).⎕NL ¯9 ⍝ This may have (mostly for Pictures) other hidden Dyalog (drawing) objects contained within it
⍝         :AndIf ×≢hidden/⍨←∧/(1↓¨hidden)∊¨⊂⎕D ⍝ Look for _999-style native objects
⍝             ##.∆EX⍕¨object ⎕VGET hidden
⍝         :EndIf
⍝         ##.∆EX⍕object.⎕VGET hidden
⍝     :EndFor
⍝     :If ×≢hidden←'_'object.⎕NL ¯9 ⍝ Other hidden Dyalog objects
⍝     :AndIf ×≢hidden←(∧/¨(1↓¨hidden)∊¨⊂⎕D)/hidden ⍝ Look for _999-style native objects
⍝         ##.∆EX⍕¨object ⎕VGET hidden
⍝     :EndIf
⍝ :EndSection
