 result←object extent arg;size;extent;where;scale;border;pscale;units;psize;dthick;Borders;margins;t;tunits;punits;dyposn;dysize
⍝ Request or modify the size of a GUI object

⍝ Note: 'where' is outside dimensions, relative to parent and using parent's scale
⍝       'size' is inside (client area) dimensions, less internal margins, using current scale
⍝       'extent' is inside (client area) dimensions, but using parent's scale
⍝ While object is closed:
⍝       Assign where←2⍴ leaves it as 2⍴ and doesn't change size/extent
⍝       Assign where←4⍴ changes size/extent to ⍬
⍝       Assign size←2⍴ or extent←2⍴, truncates where to 2⍴
⍝       Scale≠0 just doesn't store elements #2-#5 ⍝? Or does it?? Ever??
⍝       Upon opening, all are resynchronized

 :If (1=≢arg)∧1<|≡arg ⋄ arg←⊃arg ⋄ :EndIf ⍝ Make sure a nested argument is disclosed

 :Select ≢arg

 :Case 0
     result←object ⎕VGET'_extent'⍬ ⍝ Where is it currently located?

 :Case 2
     '∆WI DOMAIN ERROR: extent'##.Error∨/0>arg
     :Section ⍝ Get all our basic information
         (where extent size scale border)←object ⎕VGET('_where'⍬)('_extent'⍬)('_size'⍬)('_scale' 1)('_border' 0) ⍝ My current settings
         (psize pscale)←(⊃object.Ancestry)⎕VGET('_size'⍬)('_scale' 1) ⍝ Parent's 'size' & 'scale' (for use by 'where' and 'extent')
         units←GUIref._units ⍝ Get system conversion factors
         tunits←(1⍪units)[1+⊃scale;] ⍝ Compute unit-conversions for this object's 'scale'
         punits←(1⍪units)[1+⊃pscale;] ⍝ Compute unit-conversions for the parent object's 'scale'
         Borders←_supp.GetBorders object ⍝ Time to figure out what the border definitions are for this class (capitalized because it represents a global)
     :EndSection

     :Section ⍝ Recompute APL+Win 'extent'
         extent←arg ⍝ We'd better just be able to assign this
     :EndSection
     :Section ⍝ Recompute APL+Win 'where'
         margins←+/2 2⍴+⌿Borders[1;1 3;] ⍝ Differences between 'extent' and 'where' (in pixels)
         where←(2↑where),extent+margins÷punits ⍝ We'll just compute the new size for 'where', using a common scale
     :EndSection
     :Section ⍝ Recompute APL+Win 'size'
         :If 0≠⊃scale ⍝ If we're in proportional scaling, we don't change 'size' at all
             margins←+/2 2⍴+⌿Borders[1;,2;] ⍝ Differences between 'extent' and 'size' (in pixels)
             size←tunits÷⍨margins-⍨extent×punits ⍝ Recompute 'size', rescaling as needed
         :EndIf
     :EndSection
     :Section ⍝ Set APL+Win properties
         :If 5 6∊⍨⊃scale ⋄ size←⌊0.5+size ⋄ :EndIf ⍝ If 'scale' is in pixels, round to integers
         :If 5 6∊⍨⊃pscale ⋄ (where extent)←⌊0.5+where extent ⋄ :EndIf ⍝ If parent 'scale' is in pixels, round to integers
         object ⎕VSET('_where'where)('_size'size)('_extent'extent) ⍝ Set new APL+Win reference values
⍝? Make adjustments for closed object handling
     :EndSection

     :Section ⍝ Compute and set Dyalog positioning properties
         dyposn←Borders[2;3;1 3]+Borders[2;1;1 3]-⍨Borders[1;1;1 3]+punits×where[1 2] ⍝ Where should the Dyalog Posn start?
         dysize←extent×punits
         :If 'User'≡object ∆WG'Coord' ⍝ In this case, we've got to do something about using custom coordinates (used for Pictures)
             ##.Halt'How do we need to handle this??'
             ⍝? How can I relocate the Picture parent when its Coord=User? Does it use the parent's Coord?
             ⍝? What happens if there's a Picture within a Picture?
         :EndIf
         object ∆WSet('Posn'dyposn)('Size'dysize)
     :EndSection
     result←NoRes

 :Else
     ##.Error'∆WI LENGTH ERROR: extent'

 :EndSelect
