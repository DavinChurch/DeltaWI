 result←object Defer arg;force
⍝ Allow the application to simulate processing an event, but delayed until the system is idle
⍝ A result cannot be returned here because execution happens asynchronously later.

 'LENGTH ERROR'##.Error~arg≢⍛∊⍳5
 '∆WI DOMAIN ERROR: Event'##.Error~##.IsInt force←5⊃arg,5⍴0
 '∆WI LENGTH ERROR: Event'##.Error~force⍴⍛∊,¨⍬ 1
 :If arg[1]∊⍬ 1⍴¨0 ⋄ result←NoRes ⋄ :Return ⋄ :EndIf ⍝ Don't bother doing anything if no handler is given
 'DOMAIN ERROR'##.Error~##.IsChar 1⊃arg

 ⍝ Must pre-set 'Event' on Dyalog object to capture #9524
 :If ~'on9524'⊂⍛∊⊃¨object ##.∆WG 'Event' ⍝ If it's not set up yet, we need to add trapping for it
     object ##.∆WS⊂'Event' 'on9524'(⎕THIS⍕⍛,'._on_9524') ⍝ Add this special event to the object
 :EndIf
 result←NoRes⊣0 ##.∆NQ object 9524 ##.Application,arg ⍝ Enqueue the event for later execution by ##.Event, passing the Defer argument as an event message
