 result←object New arg;parent;name;actions;where;props;newobj;psize;units;pscale;size;extent;∆where;∆size;∆extent;ppxsize;punits;margin
⍝ Create a new Button object

 (parent name)←object ⍝ Separate non-standard two-part argument
 actions←⊃¨⊆¨arg ⍝ The names of the actions that they've asked to create this object with
 units←##.##.GUIroot._units ⍝ We'll want this for scale conversions

 ⍝ Optionally prepend object defaults (in reverse order) to be handled before applying their actual inputs
 (∆where ∆size ∆extent)←∊¨((1↓¨arg),⊂⍬)[actions⍳'where' 'size' 'extent'] ⍝ Which of our defaults are they giving us anyway (so we can skip adding defaults)
⍝? Do we want a New.* subroutine to auto-calculate centering a new object on its parent? Or perhaps preset defaults from New.New before we get here?
 :If 4≠≢where←∆where ⍝ If they're not giving us a full 4⍴ "where", generate a default they can modify later
     :If 2≠≢size←∆size ⋄ size←_size ⋄ :EndIf ⍝ Default button size (always in character units)
     (psize pscale ppxsize)←parent ⎕VGET'_size' '_scale'('_pix_size'(0 0)) ⍝ What's the parent's size & scale? ⍝? Does this give us the right data for scale=0?
     punits←(units⍪⍨ppxsize÷|2↑3↓pscale)[1+⊃pscale;] ⍝ Compute unit-conversions for the parent object's scale
     :If 2≠≢extent←∆extent ⍝ Use an extent they gave us if we have one, otherwise calculate it
         extent←size×units[1;]÷punits ⍝ Compute extent using parent's scale
     :EndIf
     :If 2=≢where ⍝ If they've supplied a partial 'where' (2⍴), then just modify it in-place to append the extent to it
         arg←(⊂'where'(where,extent))@(actions⍳⊂'where')⊢arg
     :Else ⍝ Otherwise, prepend a full default value
         margin←1 0÷punits ⍝ For some reason, an APL+Win button is centered using an extent that is 1 pixel too tall
         where←(2÷⍨psize|⍛-extent+margin),extent
         arg,⍨←⊂'where'where ⍝ [2] Start us with a default 'where' extent and position (APL+Win measure) -- will usually be overridden later
     :EndIf
     ⍝? We may also remove any existing 'extent' (or 'size') from their command line so we don't re-execute it? But is it worth it?
 :EndIf
 :If ~'caption'⊂⍛∊actions ⍝ If they're not setting a caption immediately at all, we'll put in a default
     arg,⍨←⊂'caption'name ⍝ [1] Use the object name as a caption, though this will almost certainly be changed later by the application
 :EndIf

 props←'Button'('Style' 'Push')('Visible' 0) ⍝ Properties that must be used during creation
 newobj←⎕VGET(parent⍕⍛,'.',name)##.##.∆WC props ⍝ Create the actual Dyalog object
 ⍝ While we have the object creation data in hand, let's set up the required identification and connectivity variables
 newobj.(_self _name _class)←(parent._self,'.',name)name'Button' ⍝ Set ∆WI control information
 newobj.(_Ancestry _Class)←(parent,parent._Ancestry)⎕THIS ⍝ Set ∆WI control information

 result←newobj ##.New arg ⍝ Now call the standard Classes.New routine to complete the creation work
