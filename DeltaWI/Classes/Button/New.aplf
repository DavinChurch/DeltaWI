 result←object New arg;parent;name;actions;given;where;props;newobj;psize;units;pscale;size;extent
⍝ Create a new Button object

 (parent name)←object ⍝ Separate non-standard two-part argument
 actions←⊃¨⊆¨arg ⍝ The names of the actions that they've asked to create this object with
 units←##.##.GUIroot._units ⍝ We'll want this for scale conversions

 ⍝ Optionally prepend object defaults (in reverse order) to be handled before applying their actual inputs
 given←∊¨((1↓¨arg),⊂⍬)[actions⍳'where' 'size' 'extent'] ⍝ Which of our defaults are they giving us anyway (so we can skip adding defaults)
⍝? Do we want a New.* subroutine to auto-calculate centering a new object on its parent?
 :If 4≠≢where←1⊃given ⍝ If they're not giving us a full 4⍴ "where", generate a default they can modify later
     :If 2≠≢size←2⊃given ⋄ size←_size ⋄ :EndIf ⍝ Default button size (always in character units)
     (psize pscale)←parent.(_size _scale) ⍝ What's the parent's size & scale?
     'How do we figure the new position when the parent (and maybe their parent) is proportional?'##.##.Halt 0=⊃pscale ⍝? A New.RealSize function?
     :If 2≠≢extent←3⊃given ⍝ Use an extent they gave us if we have one, otherwise calculate it
         ⍝ For some reason, an APL+Win button is centered using an extent that is 1/16 of a character too big? Is it to leave room for a old-style drop shadow?
         extent←size×÷⌿units[1,⊃pscale;] ⍝ Compute extent using parent's scale
     :EndIf
     :If 2=≢where ⍝ If they've supplied a partial 'where' (2⍴), then just modify it in-place to append the extent to it
         arg←(⊂'where'(where,extent))@(actions⍳⊂'where')⊢arg
     :Else ⍝ Otherwise, prepend a full default value
         where←(0.5×psize-extent+2↑÷16),extent
         arg,⍨←⊂'where'where ⍝ [2] Start us with a default 'where' extent and position (APL+Win measure) -- will usually be overridden later
     :EndIf
     ⍝? We may also remove any existing 'extent' (or 'size') from their command line so we don't re-execute it? But is it worth it?
 :EndIf
 :If ~'caption'⊂⍛∊actions ⍝ If they're not setting a caption immediately at all, we'll put in a default
     arg,⍨←⊂'caption'name ⍝ [1] Use the object name as a caption, though this will almost certainly be changed later by the application
 :EndIf

 props←'Button'('Style' 'Push')('Visible' 0) ⍝ Properties that must be used during creation
 newobj←⎕VGET((⍕parent),'.',name)##.##.∆WC props ⍝ Create the actual Dyalog object
 ⍝ While we have the object creation data in hand, let's set up the required identification and connectivity variables
 newobj.(_self _name _class)←(parent._self,'.',name)name'Button' ⍝ Set ∆WI control information
 newobj.(_Ancestry _Class)←(parent,parent._Ancestry)⎕THIS ⍝ Set ∆WI control information

 result←newobj ##.New arg ⍝ Now call the standard Classes.New routine to complete the creation work
