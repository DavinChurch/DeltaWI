 result←object New arg;parent;name;actions;props;newobj
⍝ Create a new Button object

 (parent name)←object ⍝ Separate non-standard two-part argument
 actions←⊃¨⊆¨arg ⍝ The names of the actions that they've asked to create this object with

 arg←(parent:parent ⋄ offset:1 0) ##.##.New.Center arg
:If ~'caption'⊂⍛∊actions ⍝ If they're not setting a caption immediately at all, we'll put in a default
     arg,⍨←⊂'caption'name ⍝ [1] Use the object name as a caption, though this will almost certainly be changed later by the application
 :EndIf

 props←'Button'('Style' 'Push')('Visible' 0) ⍝ Properties that must be used during creation
 newobj←⎕VGET(parent⍕⍛,'.',name)##.##.∆WC props ⍝ Create the actual Dyalog object
 ⍝ While we have the object creation data in hand, let's set up the required identification and connectivity variables
 newobj.(_self _name _class)←(parent._self,'.',name)name'Button' ⍝ Set ∆WI control information
 newobj.(_Ancestry _Class)←(parent,parent._Ancestry)⎕THIS ⍝ Set ∆WI control information

 result←newobj ##.New arg ⍝ Now call the standard Classes.New routine to complete the creation work
