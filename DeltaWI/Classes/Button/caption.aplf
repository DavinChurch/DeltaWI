 result←object caption arg;old;new
⍝ Read or change the caption on the Button

 old←object ⎕VGET⊂'_caption' '' ⍝ Existing caption
 :If 0=≢arg
     result←old
 :Else
     '∆WI DOMAIN ERROR: caption'##.##.Error 1≠≢arg
     '∆WI DOMAIN ERROR: caption'##.##.Error(1≠≡new)∨1≠≢⍴new←1/##.##.Single arg ⍝ New caption to be shown
     '∆WI DOMAIN ERROR: caption'##.##.Error~##.##.IsChar new
     object._caption←new ⍝ Update the APL+Win record
     object ##.##.∆WS'caption'((⎕UCS 10)@{⍵=⎕UCS 13}⊢new) ⍝ Change the Dyalog object so the screen actually changes
⍝? Does changing the Dyalog button caption auto-change the button size?
     result←##.NoRes
 :EndIf
