 result←object New arg;parent;name;props;newobj;actions
⍝ Create a new Label object

 (parent name)←object ⍝ Separate non-standard two-part argument
 actions←⊃¨⊆¨arg ⍝ The names of the actions that they've asked to create this object with

 arg,⍨←(~'caption'⊂⍛∊actions)/⊂'caption' name
 arg←(parent:parent ⋄ stretch:1 0)##.##.New.Center arg

 props←'Label'('FieldType' 'Char')('Visible' 0) ⍝ Properties that must be used during creation
 newobj←⎕VGET(parent⍕⍛,'.',name)##.##.∆WC props ⍝ Create the actual Dyalog object
 result←parent newobj ##.##.New.Initialize arg ⍝ Complete standard class-initialization procedure
