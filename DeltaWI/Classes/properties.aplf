 result←object properties arg;lower;private;public;override;udp
⍝ Produce a list of available properties for this object

 '∆WI ACTION ERROR: Property "properties" is read-only'##.Error×≢arg
 lower←⎕C ⎕A
 private←lower object._Class.⎕NL ¯3 ⍝ Class-specific properties
 private~←override←((⊂1 2 0)≢¨⊃¨object._Class.⎕AT¨private)/private ⍝ Class-specific suppression of generic properties
 public←override~⍨lower ⎕NL ¯3 ⍝ Accessible class-generic properties
 udp←{⍵/⍨'∆'=⊃¨⍵}1↓¨'_'object.⎕NL ¯2 ⍝ User-defined properties
 result←{⍵[⍋⍵]}{'⎕'@('⍙'=⊢)⍵}¨{⍵/⍨~'on'⊂⍛∊2↑¨⍵}private∪public∪udp ⍝ Combine & clean up lists
