 result←object where arg;size;extent;where;scale;border;pscale;units;psize;dthick;Borders;margins;t;tunits;punits;dyposn;dysize
⍝ Request or modify the location and size of a GUI object

⍝ Note: 'where' is outside dimensions, relative to parent and using parent's scale
⍝       'size' is inside (client area) dimensions, less internal margins, using current scale
⍝       'extent' is inside (client area) dimensions, but using parent's scale
⍝ While object is closed:
⍝       Assign where←2⍴ leaves it as 2⍴ and doesn't change size/extent
⍝       Assign where←4⍴ changes size/extent to ⍬
⍝       Assign size←2⍴ or extent←2⍴, truncates where to 2⍴
⍝       Scale≠0 just doesn't store elements #2-#5 ⍝? Or does it?? Ever??
⍝       Upon opening, all are resynchronized

⍝? Don't forget to fire onResize event (and any other events necessary)

 :If (1=≢arg)∧1<|≡arg ⋄ arg←⊃arg ⋄ :EndIf ⍝ Make sure a nested argument is disclosed

 :Select ≢arg

 :Case 0
     result←object ⎕VGET⊂'_where'⍬ ⍝ Where is it currently located?

 :CaseList 2 4
     '∆WI DOMAIN ERROR: where'##.Error∨/0>2↑2↓arg
     :Section ⍝ Get all our basic information
         (where extent size scale border)←object ⎕VGET('_where'⍬)('_extent'⍬)('_size'⍬)('_scale' 1)('_border' 0) ⍝ My current settings
         (psize pscale)←(⊃object.Ancestry)⎕VGET('_size'⍬)('_scale' 1) ⍝ Parent's size & scale (for use by where and extent)
         units←##.GUIref._units ⍝ Get system conversion factors
         tunits←(1⍪units)[1+⊃scale;] ⍝ Compute unit-conversions for this object's scale
         punits←(1⍪units)[1+⊃pscale;] ⍝ Compute unit-conversions for the parent object's scale
         Borders←_supp.GetBorders object ⍝ Time to figure out what the border definitions are for this class (capitalized because it represents a global)
⍝         :If 0 ⍝ Don't use this code - ignore it until we capture any comments in new "Form" code
⍝             ⍝ [Strange notes:
⍝             ⍝  Border=2 (w/caption) shifts the visible image up and left by a few pixels and leaves a little
⍝             ⍝  bottom/right margin inside the borders, but these are not reflected in 'where' or 'extent' at all!
⍝             ⍝  This shifting does not happen when the caption is off.
⍝             ⍝  Border=0 (w/o caption) shows a visible border (3pix?) anyway, but not shown in 'where'/'extent'.
⍝             ⍝  Are these effects caused by default 3D-controls in Windows?
⍝             ⍝ ]
⍝             ⍝ MDIForms don't change measurement behavior if they don't have a caption.
⍝                     :If (×≢caption)∨object._class≡'MDIForm'
⍝                         a_outside←26 3 3 3+5×2=4|border
⍝                     :Else
⍝                         a_outside←0 0 0 0+(2|border)+7×2=4|border
⍝                     :EndIf
⍝                 :CaseList 'DateTime' 'Edit' 'List' 'Listview' 'RichEdit' 'Tree'
⍝                     a_inside←4⍴2 ⍝ Some have large internal borders
⍝                 :Case 'Progress'
⍝                     a_inside←4⍴1 ⍝ Some have small internal borders
⍝                 :Case 'Selector'
⍝                     a_inside←4⍴4 ⍝ Some have extra-large internal borders
⍝                 :Case 'Status'
⍝                     a_outside←a_inside←4⍴0 ⍝ Some have fixed positions & sizes
⍝                 :EndSelect
⍝         :EndIf
     :EndSection

     :Section ⍝ Recompute APL+Win 'where'
         where←arg,(2=≢arg)/2↓where ⍝ Change starting position, and size if it's given
         ⍝ If we don't have a new (external) size, figure it out as best we can
         :If (2=≢where)∧×≢extent ⍝ If we need 'where' completed and have an 'extent', work from that
             margins←+/2 2⍴+⌿Borders[1;1 3;] ⍝ Differences between 'extent' and 'where' (in pixels)
             where,←extent+margins÷punits ⍝ We'll just compute the rest of 'where', using a common scale
         :EndIf
         :If (2=≢where)∧(×≢size)∧0≠⊃scale ⍝ If we need where completed and have a size, work from that
             margins←+/2 2⍴+⌿Borders[1;1 2 3;] ⍝ Differences between size and where (in pixels)
             where,←punits÷⍨margins+size×tunits ⍝ We'll just compute the rest of where, rescaling as needed
         :EndIf
     :EndSection
     :Section ⍝ Recompute APL+Win 'extent'
         :If (4=≢where)∧(2<≢arg)∨0=≢extent ⍝ If we haven't been given a new where-size, and we have an old 'extent', then there's no point in recomputing
             ⍝ We'll compute 'extent' from our newly-derived or -provided 'where'
             margins←+/2 2⍴+⌿Borders[1;1 3;] ⍝ Differences between 'extent' and 'where' (in pixels)
             extent←where[3 4]-margins÷punits
         :EndIf
         :If (2=≢size)∧(0=≢extent)∧0≠⊃scale ⍝ If we have a 'size' but not an 'extent', recompute from what we do have
             margins←+/2 2⍴+⌿Borders[1;,2;] ⍝ Differences between 'extent' and 'size' (in pixels)
             extent←punits÷⍨margins+size×tunits ⍝ Recompute 'extent', rescaling as needed
         :EndIf
     :EndSection
     :Section ⍝ Recompute APL+Win 'size'
         :If (0≠⊃scale)∧2=≢extent ⍝ If we're in proportional scaling, we don't change 'size' at all
             margins←+/2 2⍴+⌿Borders[1;,2;] ⍝ Differences between 'extent' and 'size' (in pixels)
             size←tunits÷⍨margins-⍨extent×punits ⍝ Recompute 'size', rescaling as needed
         :EndIf
     :EndSection
     :Section ⍝ Set APL+Win properties
         :If 5 6∊⍨⊃scale ⋄ size←⌊0.5+size ⋄ :EndIf ⍝ If 'scale' is in pixels, round to integers
         :If 5 6∊⍨⊃pscale ⋄ (where extent)←⌊0.5+where extent ⋄ :EndIf ⍝ If parent 'scale' is in pixels, round to integers
         object ⎕VSET('_where'where)('_size'size)('_extent'extent) ⍝ Set new APL+Win reference values
⍝? Make adjustments for closed object handling
     :EndSection

     :Section ⍝ Compute and set Dyalog positioning properties
         dyposn←(+⌿Borders[2;1 3;1 3])+punits×where[1 2] ⍝ Where should the Dyalog Posn start?
         dysize←extent×punits
         :If 'User'≡object ##.∆WG'Coord' ⍝ In this case, we've got to do something about using custom coordinates (used for Pictures)
             ##.Halt'How do we need to handle this??'
             ⍝? How can I relocate the Picture parent when its Coord=User? Does it use the parent's Coord?
             ⍝? What happens if there's a Picture within a Picture?
         :EndIf
         object ##.∆WSet('Posn'dyposn)('Size'dysize)
     :EndSection
     result←NoRes

 :Else
     ##.Error'∆WI LENGTH ERROR: where'

 :EndSelect
