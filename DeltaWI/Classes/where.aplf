 result←object where arg;size;extent;where;scale;border;pscale;units;psize;dthick;Borders;margins;t;tunits;punits;dyposn;dysize;resize;ppxsize;pxsize;at;rewhere
⍝ Request or modify the location and size of a GUI object

⍝ Note: 'where' is outside dimensions, relative to parent and using parent's scale
⍝       'size' is inside (client area) dimensions, less internal margins, using current scale
⍝       'extent' is inside (client area) dimensions, but using parent's scale
⍝ While object is closed:
⍝       Assign where←2⍴ leaves it as 2⍴ and doesn't change size/extent
⍝       Assign where←4⍴ changes size/extent to ⍬
⍝       Assign size←2⍴ or extent←2⍴, truncates where to 2⍴
⍝       Scale≠0 just doesn't store elements #2-#5
⍝       Upon opening, all are resynchronized ⍝? Do we need to do this?

⍝? Caution: You cannot change the position or size of an object if it is minimized or maximized!

 arg←##.Single arg ⍝ Make sure a nested argument is disclosed

 :Select ≢arg

 :Case 0
     result←object ⎕VGET⊂'_where'⍬ ⍝ Where is it currently located?

 :CaseList 2 4
     '∆WI DOMAIN ERROR: where'##.Error∨/0>2↑2↓arg
     :Section ⍝ Get all our basic information
         (where extent size scale border pxsize)←object ⎕VGET('_where'⍬)('_extent'⍬)('_size'⍬)('_scale' 1)('_border' 0)('_pix_size'(0 0)) ⍝ My current settings
         (psize pscale ppxsize)←(⊃object._Ancestry)⎕VGET('_size'⍬)('_scale' 1)('_pix_size'(0 0)) ⍝ Parent's size & scale (for use by where and extent)
         units←##.GUIroot._units ⍝ Get system conversion factors
         tunits←(1⍪units)[1+⊃scale;] ⍝ Compute unit-conversions for this object's scale ⍝? Need scale=0 adjusting
         punits←(units⍪⍨ppxsize÷|2↑3↓pscale)[1+⊃pscale;] ⍝ Compute unit-conversions for the parent object's scale
         Borders←_supp.GetBorders object ⍝ Time to figure out what the border definitions are for this class (capitalized because it represents a global)
         (rewhere resize)←where size ⍝ Remember these
⍝         :If 0 ⍝ Don't use this code - ignore it until we capture any comments in new "Form" code
⍝             ⍝ [Strange notes:
⍝             ⍝  Border=2 (w/caption) shifts the visible image up and left by a few pixels and leaves a little
⍝             ⍝  bottom/right margin inside the borders, but these are not reflected in 'where' or 'extent' at all!
⍝             ⍝  This shifting does not happen when the caption is off.
⍝             ⍝  Border=0 (w/o caption) shows a visible border (3pix?) anyway, but not shown in 'where'/'extent'.
⍝             ⍝  Are these effects caused by default 3D-controls in Windows?
⍝             ⍝ ]
⍝             ⍝ MDIForms don't change measurement behavior if they don't have a caption.
⍝                     :If (×≢caption)∨object._class≡'MDIForm'
⍝                         a_outside←26 3 3 3+5×2=4|border
⍝                     :Else
⍝                         a_outside←0 0 0 0+(2|border)+7×2=4|border
⍝                     :EndIf
⍝                 :CaseList 'DateTime' 'Edit' 'List' 'Listview' 'RichEdit' 'Tree'
⍝                     a_inside←4⍴2 ⍝ Some have large internal borders
⍝                 :Case 'Progress'
⍝                     a_inside←4⍴1 ⍝ Some have small internal borders
⍝                 :Case 'Selector'
⍝                     a_inside←4⍴4 ⍝ Some have extra-large internal borders
⍝                 :Case 'Status'
⍝                     a_outside←a_inside←4⍴0 ⍝ Some have fixed positions & sizes
⍝                 :EndSelect
⍝         :EndIf
     :EndSection

     :Section ⍝ Recompute APL+Win 'where'
         where←arg,(2=≢arg)/2↓where ⍝ Change starting position, and size if it's given
         ⍝ If we don't have a new (external) size, figure it out as best we can
         :If (2=≢where)∧×≢extent ⍝ If we need 'where' completed and have an 'extent', work from that
             margins←+/2 2⍴+⌿Borders[1;1 3;] ⍝ Differences between 'extent' and 'where' (in pixels)
             where,←extent+margins÷punits ⍝ We'll just compute the rest of 'where', using a common scale
         :EndIf
         :If (2=≢where)∧(×≢size)∧0≠⊃scale ⍝ If we need where completed and have a size, work from that
             margins←+/2 2⍴+⌿Borders[1;1 2 3;] ⍝ Differences between size and where (in pixels)
             where,←punits÷⍨margins+size×tunits ⍝ We'll just compute the rest of where, rescaling as needed
⍝? This isn't working when converting units??
         :EndIf
     :EndSection
     :Section ⍝ Recompute APL+Win 'extent'
         :If (4=≢where)∧(2<≢arg)∨0=≢extent ⍝ If we haven't been given a new where-size, and we have an old 'extent', then there's no point in recomputing
             ⍝ We'll compute 'extent' from our newly-derived or -provided 'where'
             margins←+/2 2⍴+⌿Borders[1;1 3;] ⍝ Differences between 'extent' and 'where' (in pixels)
             extent←where[3 4]-margins÷punits
         :EndIf
         :If (2=≢size)∧(0=≢extent)∧0≠⊃scale ⍝ If we have a 'size' but not an 'extent', recompute from what we do have
             margins←+/2 2⍴+⌿Borders[1;,2;] ⍝ Differences between 'extent' and 'size' (in pixels)
             extent←punits÷⍨margins+size×tunits ⍝ Recompute 'extent', rescaling as needed
         :EndIf
     :EndSection
     :Section ⍝ Recompute APL+Win 'size'
         :If (0≠⊃scale)∧2=≢extent ⍝ If we're in proportional scaling, we don't change 'size' at all
             margins←+/2 2⍴+⌿Borders[1;,2;] ⍝ Differences between 'extent' and 'size' (in pixels)
             size←tunits÷⍨margins-⍨extent×punits ⍝ Recompute 'size', rescaling as needed
         :EndIf
     :EndSection
     :Section ⍝ Set APL+Win properties
         :If scale⊃⍛∊5 6 ⋄ size←⌊0.5+size ⋄ :EndIf ⍝ If 'scale' is in pixels, round to integers
         :If pscale⊃⍛∊5 6 ⋄ (where extent)←⌊0.5+where extent ⋄ :EndIf ⍝ If parent 'scale' is in pixels, round to integers
         object.(_where _size _extent)←where size extent ⍝ Set new APL+Win reference values
⍝? Make adjustments for closed object handling
     :EndSection

     :Section ⍝ Compute and set Dyalog positioning properties
         at←|(where[1 2]-(2↑1↓pscale)×psize>0)+psize×psize<0 ⍝ Figure the Dyalog-relative positioning for offset & inverted axes
         dyposn←(+⌿Borders[2;1 3;1 3])+punits×at ⍝ Where should the Dyalog Posn start?
         dysize←punits×extent
         :If 'User'≡object ##.∆WG'Coord' ⍝ In this case, we've got to do something about using custom coordinates (used for Pictures)
             ##.Halt'How do we need to handle this??'
             ⍝? How can I relocate the Picture parent when its Coord=User? Does it use the parent's Coord?
             ⍝? What happens if there's a Picture within a Picture?
         :EndIf
         object ##.∆WSet('Posn'dyposn)('Size'dysize)
     :EndSection

     :If (2↑where,'⊢⊣')≢2↑rewhere,'⊣⊢'
         'Move'##.Event object ##.Application,where[1 2] ⍝ Run the onMove event handler
     :EndIf
     :If size≢resize ⍝ Only if the size is changing
         'Resize'##.Event object ##.Application,object._size,0 ⍝ Run the onResize event handler
     :EndIf
     result←NoRes

 :Else
     ##.Error'∆WI LENGTH ERROR: where'

 :EndSelect
