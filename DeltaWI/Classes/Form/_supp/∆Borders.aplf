 borders←∆Borders object;border;caption
⍝ Specify borders that will be used by APL+Win and Dyalog Form objects
⍝ Factors will be dependent upon current object settings
⍝? Will this run after the object settings have been made? Will it need recomputing if they change?

 borders←[ ⍝ Set up default 3⍴⍴ structure
     [ ⍝ Layer [1;;] describes APL+Win Forms
         0 0 0 0
         0 0 0 0
         0 0 0 0
     ]
     [ ⍝ Layer [2;;] describes Dyalog Forms
         0 0 0 0
         0 0 0 0
         0 0 0 0
     ]
 ]

 (border caption)←object ⎕VGET('_border' 0)('_caption' '') ⍝ Check important properties
 border←##.##.##.BitVals+/border ⍝ Convert to bit flags for analysis

 :Section ⍝ Build APL+Win settings
     :If ×≢caption ⍝ If they gave us a caption, we must allow room for it regardless
         borders[1;3;]←23 0 0 0 ⍝ Enough space for the caption
         :If </1 2∊border ⍝ Did they ask for a thick border?
⍝? Visually, this doesn't change in this way:
⍝? ...the interior and exterior get about 2px smaller on each edge without affecting the visible border thicknesses, but the center-point remains the same
⍝? Might this be due to a Windows control version difference??
             borders[1;1;]←8 8 8 8 ⍝ We'll need a thick edge line
         :Else
             borders[1;1;]←3 3 3 3 ⍝ A relatively thin edge line will do
         :EndIf
     :Else
         borders[1;3;]←0 0 0 0 ⍝ No space for a caption
         :If 1∊border ⍝ (includes obsolete border=3)
             borders[1;1;]←1 1 1 1 ⍝ Pixel-width edge line
         :ElseIf 2∊border
             borders[1;1;]←8 8 8 8 ⍝ Thick edge line
         :Else
             borders[1;1;]←0 0 0 0 ⍝ No edge line
         :EndIf
     :EndIf
 :EndSection

 :Section ⍝ Build Dyalog settings
     ⍝ Since Dyalog Posn sets the client area, put all the decorations in the "outside border" area rather than the "inside border" like APL+Win
     :If (×≢caption)∨∨/border≥16 ⍝ Have they asked for a caption or buttons that require a title bar?
         borders[2;1;]←31 8 8 8 ⍝ Room for a title bar and thick borders, regardless of what else they asked for
     :Else ⍝ We won't be using a title bar at all
         borders[2;1;]←4⍴+/(<\1 2∊border)/1 3 ⍝ Select thickness of minimal ⌈border (0, 1, or 3 pixels)
     :EndIf
     ⍝ Leave the remaining settings at 0
 :EndSection
