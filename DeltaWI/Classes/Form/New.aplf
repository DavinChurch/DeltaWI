 result←object New arg;parent;name;actions;where;newobj;given
⍝ Create a new Form object

 (parent name)←object ⍝ Separate non-standard two-part argument
 arg←1↓arg ⍝ Ignore the class name, we know what it is
 actions←⊃¨arg ⍝ The names of the actions that we'll be using to create this object with
 ⍝? Just reminders for us to use elsewhere - not needed here
 ⍝only←'VScroll' 'HScroll' ⍝ These properties may only be set during ∆WC
 ⍝(border style)←0~⍨¨##.##.BitVals¨+/¨∊¨(0,⍨1↓¨arg)[actions⍳'border' 'style'] ⍝ Get the bit-flag values they've provided on this line

 ⍝ Optionally prepend object defaults (in reverse order) to be handled
 given←((1↓¨arg),⊂⍬)[actions⍳'where' 'caption'] ⍝ Which of our defaults are they giving us anyway (so we can skip the defaults)
 :If 4≠≢∊1⊃given
     where←,parent._size÷⍤1⊢2 2⍴4 4 2 2 ⍝ Default APL+Win Form size and position ⍝? Do we need to mess with any border sizes here?
     arg,⍨←⊂'where'where ⍝ [2] Start us with a default 'where' size and position (APL+Win measure) -- will usually be overridden later
 :EndIf
 :If ~'caption'∊⍥⊆actions ⍝ If they're not setting a caption immediately, we'll put in a default
     arg,⍨←⊂'caption'name ⍝ [1] Use the object name as a caption, though this will almost certainly be changed later by the application
 :EndIf
 ⍝? This sequence may cause a little flickering, but see if it's a problem before we look into changing it

 newobj←((⍕parent),'.',name)##.##.∆WC'Form'('Visible' 0) ⍝ Create the actual Dyalog object
 newobj.(_self _name _class)←(parent._self,'.',name)name'Form' ⍝ Set ∆WI control information
 newobj.(Ancestry Class)←(parent.Ancestry,parent)⎕THIS ⍝ Set ∆WI control information

 result←newobj ##.New arg ⍝ Now call the standard Classes.New routine to complete the creation work
