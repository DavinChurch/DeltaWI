 result←object New arg;parent;name;actions;∆style;props;newobj
⍝ Create a new Edit object

 (parent name)←object ⍝ Separate non-standard two-part argument
 actions←⊃¨⊆¨arg ⍝ The names of the actions that they've asked to create this object with

 arg←(parent:parent ⋄ offset:1 0)##.##.New.Center arg
 ∆style←⊃((1↓¨arg),⊂⍬)[actions⍳⊂'style']

 props←'Edit'('FieldType' 'Char')('Visible' 0) ⍝ Properties that must be used during creation
 props,←⊂'Style'('Single' 'Multi'⊃⍨1+2 ##.##.BitOf+/∊∆style) ⍝ Set the starting style if we know it ⍝? This cannot be changed once created!
 newobj←⎕VGET(parent⍕⍛,'.',name)##.##.∆WC props ⍝ Create the actual Dyalog object
 ⍝ While we have the object creation data in hand, let's set up the required identification and connectivity variables
 newobj.(_self _name _class)←(parent._self,'.',name)name'Edit' ⍝ Set ∆WI control information
 newobj.(_Ancestry _Class)←(parent,parent._Ancestry)⎕THIS ⍝ Set ∆WI control information

 result←newobj ##.New arg ⍝ Now call the standard Classes.New routine to complete the creation work
