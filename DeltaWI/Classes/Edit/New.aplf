 result←object New arg;parent;name;actions;∆style;props;newobj;∆border
⍝ Create a new Edit object

 (parent name)←object ⍝ Separate non-standard two-part argument
 actions←⊃¨⊆¨arg ⍝ The names of the actions that they've asked to create this object with

 (∆style ∆border)←((1↓¨arg),⊂⍬)[actions⍳'style' 'border']
 ∆border←0⌈×+/∊∆border ⋄ ∆style←##.##.BitVals+/∊∆style
 arg←(parent:parent ⋄ border:∆border)##.##.New.Center arg
 arg,←(~'edge'⊂⍛∊actions)/⊂'edge' 1 ⍝ Only Edit boxes have a default 'edge'

 props←'Edit'('FieldType' 'Char')('Visible' 0) ⍝ Define properties that must be used during creation
 props,←⊂'Style'('Single' 'Multi'⊃⍨1+4∊∆style) ⍝? Create-only style
 props,←⊂'Justify'('Center' 'Right' 'Left'⊃⍨1⍳⍨1 2∊∆style) ⍝? Create-only style
 ⍝? Center or Right justification forces word-wrapping and disables horizontal scrolling (32∊∆style)
 props,←⊂'VScroll'((>/4 8∊∆style)×¯2+16∊∆style) ⍝? Create-only style
 props,←⊂'HScroll'((~32∊∆style)×¯2+64∊∆style) ⍝? Create-only style
 props,←(~'border'⊂⍛∊actions)/⊂'Border'∆border ⍝ Set border upon creation if we have it ⍝? It cannot be changed later
 props,←⊂'WantsReturn'(8192∊∆style)
 newobj←⎕VGET(parent⍕⍛,'.',name)##.##.∆WC props ⍝ Create the actual Dyalog object with required parameters
 result←parent newobj ##.##.New.Initialize arg ⍝ Complete standard class-initialization procedure
