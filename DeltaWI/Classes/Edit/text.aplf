 result←object text arg;old;new
⍝ Read or change the text in the Edit box
⍝ This property is not stored as an APL variable, but directly references the native object which the user can control

 :If 0=≢arg
     result←,object ##.##.∆WG'Text'
     :If 1<≡result ⋄ result←(⎕UCS 13)##.##.∆sew result ⋄ :EndIf ⍝ Assume that we never use matrix-form
     result←('←',⊃object ⎕VGET⊂'_font' '')##.##.QuadAV result ⍝ Reverse-engineer the on-screen font back to Unicode, as best possible, if needed
 :Else
     '∆WI DOMAIN ERROR: text'##.##.Error 1≠≢arg
     new←1/##.##.Single arg
     '∆WI RANK ERROR: text'##.##.Error 1≠≢⍴new
     '∆WI DOMAIN ERROR: text'##.##.Error 1≠≡new
     '∆WI DOMAIN ERROR: text'##.##.Error~##.##.IsChar new
     :If 2 ##.##.BitOf object ⎕VGET⊂'_style' 0
         new←(⎕UCS 13)##.##.∆cuts new ⍝ Turn it into a Dyalog multi-line structure (we'll never use matrix-form)
     :EndIf
     new←object ##.##.QuadAV new ⍝ Convert Unicode to ⎕AV as best we can if we're using one of those fonts
     object ##.##.∆WS⊂'Text'new
     result←##.NoRes

     ⍝ Now we need to fire an implied onChange event for them
     'Change'##.##.Event object ##.##.Application
 :EndIf
