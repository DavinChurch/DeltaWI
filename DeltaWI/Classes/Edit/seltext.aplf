 result←object seltext arg;txt;seltxt;new
⍝ Edit:selection

 txt←object text ⍬ ⋄ seltxt←object selection ⍬ ⍝ Get existing properties the easy way
 :If 0=≢arg
     result←seltxt[2]↑seltxt[1]↓txt
 :Else
     '∆WI DOMAIN ERROR: seltext'##.##.Error 1≠≢arg
     new←1/##.##.Single arg
     '∆WI RANK ERROR: seltext'##.##.Error 1≠≢⍴new
     '∆WI DOMAIN ERROR: seltext'##.##.Error 1≠≡new
     '∆WI DOMAIN ERROR: seltext'##.##.Error~##.##.IsChar new
     new←(⊃object ⎕VGET⊂'_font' '')##.##.QuadAV new ⍝ Convert Unicode to ⎕AV as best we can if we're using one of those fonts
     txt←(seltxt[1]↑txt),new,(+/seltxt)↓txt ⍝ Replace that portion of the text
     seltxt[2]←≢new ⍝ Update selected position
     {}object text,⊂txt ⋄ {}object selection,⊂seltxt ⍝ Write out the new properties the easy way
     result←##.NoRes

     ⍝ Now we need to fire an implied onChange event for them
     'Change'##.##.Event object ##.##.Application
 :EndIf
