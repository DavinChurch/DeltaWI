 result←object New arg;parent;name;props;newobj;actions;∆border;∆style
⍝ Create a new Option object

 (parent name)←object ⍝ Separate non-standard two-part argument
 actions←⊃¨⊆¨arg ⍝ The names of the actions that they've asked to create this object with

 (∆border ∆style)←((1↓¨arg),⊂⍬)[actions⍳'border' 'style']
 ∆border←0⌈×+/∊∆border ⋄ ∆style←##.##.BitVals+/∊∆style
 arg,⍨←(~'caption'⊂⍛∊actions)/⊂'caption'name ⍝ If they're not setting a caption immediately at all, we'll put in a default
 arg←(parent:parent ⋄ border:∆border)##.##.New.Center arg

 props←'Button'('Style' 'Radio'),(~1∊∆style)↓('Align' 'Left')('Visible' 0) ⍝ Properties that must be used during creation
 props,←(~'border'⊂⍛∊actions)/⊂'Border'∆border ⍝ Set border upon creation if we have it ⍝? It cannot be changed later
 newobj←⎕VGET(parent⍕⍛,'.',name)##.##.∆WC props ⍝ Create the actual Dyalog object
 result←parent newobj ##.##.New.Initialize arg ⍝ Complete standard class-initialization procedure
