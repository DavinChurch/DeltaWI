 result←object SetLinks arg;was;t
⍝ Allow writing (add, delete, or update) of one or more pseudo-child aliases to the 'links' property

 '∆WI LENGTH ERROR: SetLinks'##.Error 2<≢arg
 :If 1<≢arg ⋄ arg←1 2⍴arg ⋄ :EndIf ⍝ Allow a single link to be given as a vector
 '∆WI RANK ERROR: SetLinks'##.Error 2≠≢⍴arg
 '∆WI LENGTH ERROR: SetLinks'##.Error 2≠2⊃⍴arg
 '∆WI DOMAIN ERROR: SetLinks'##.Error 0∊##.IsChar¨arg
 '∆WI RANK ERROR: SetLinks'##.Error 0∊1=≢¨⍴¨arg
 arg←∆dlt¨arg ⍝ Ignore leading and trailing spaces
 '∆WI DOMAIN ERROR: SetLinks'##.Error 0∊##.IsValidName¨arg[;1]
 was←object ⎕VGET⊂'_links'(0 2⍴⊂'')
 t←arg[;1]∊was[;1] ⋄ was[was[;1]⍳t/arg[;1];]←t⌿arg ⋄ arg⌿⍨←~t ⍝ Update changed links in-place
 was⍪←arg ⍝ Add new links to the bottom of the list
 was⌿⍨←0≠≢¨was[;2] ⍝ Throw away any links to be deleted
 object ⎕VSET⊂'_links'was ⍝ Now we can put back the new list
 result←NoRes
